\section{Analizador léxico }

Para configurar el analizar léxico es necesario proveer expresiones regulares que reconozcan cada uno de los tokens. Para poder reconocer las palabras reservadas del lenguaje  fue necesario agregar lógica extra al analizador. Para evitar que las palabras reservadas sean usadas como variables se creo un diccionario donde cada elemento es el par (palabra reservado,token palabra reservada). Cuando una cadena del código siendo analizado se corresponde con la expresión regular del token VARIABLE, antes de aceptar el token se buscar la cadena en el diccionario. De encontrar la cadena en el diccionario se utiliza el tipo de token definido en el mismo, en caso contrario se devuelve un token VARIABLE. 
Como en etapas posteriores se verificara no solo la sintaxis, sino también que el código cumple con las restricciones de tipado del lenguaje, el lexer diferencia entre números, cadenas de caracteres y booleanos, devolviendo los tokens NUMERO,CADENA y BOOL respectivamente.

\subsection{Expresiones regulares para tokens}

\begin{verbatim}
                  DO ->    'do'
               WHILE ->    'while'
                 FOR ->    'for'
                  IF ->    'if'
                ELSE ->    'else'
                 RES ->    'res'
               RETURN ->    'return'
                BEGIN ->    'begin'
                  END ->    'end'
          CAPITALIZAR ->    'capitalizar'
               LENGTH ->    'length'
                PRINT ->    'print'
MULTIPLICACIONESCALAR ->    'multiplicacionescalar'
           COLINEALES ->    'colineales'
                  AND ->    'and'
                   OR ->    'or'
                  NOT ->    'not'
                MINUS ->    '-'
              ELEVADO ->    '^'
               MODULO ->    '%'
                  DIV ->    '/'
                MAYOR ->    '>'
                MENOR ->    '<'
                 PLUS ->    '+'
                TIMES ->    '*'
               LPAREN ->    '('
               RPAREN ->    ')'
            LCORCHETE ->    '['
            RCORCHETE ->    ']'
             LLAVEIZQ ->    '{'
             LLAVEDER ->    '}'
        INTERROGACION ->    '?'
                PUNTO ->    '.'
            DOSPUNTOS ->    ':'
           PUNTOYCOMA ->    ';'
                 COMA ->    ','
                IGUAL ->    '='('\n')*'='
             DISTINTO ->    '!'('\n')*'='
              AGREGAR ->    '+'('\n')*'='
                SACAR ->    '-'('\n')*'='
               DIVIDI ->    '/'('\n')*'='
              MULTIPL ->    '*'('\n')*'='
               MASMAS ->    '+'('\n')*'+'
           COMENTARIO ->    '#'.*
               CADENA -> '"' .*? '"'
                 BOOL ->    'true' | 'false' | 'FALSE' | 'TRUE'
             VARIABLE -> ([a-z][A-Z])([a-z][A-Z]|'_'|[0-9])*
           ASGINACION ->   '='

\end{verbatim}
\section{Análisis sintáctico}

Se procedió a crear una gramática que pudiese reconocer el lenguaje SLS. 
La herramienta utilizada para construir el analizador sintáctica,a la cual se le proveen los tokens del lexer y las producciones de la gramática, crea un analizador LALR. 

La creación de la gramática fue un proceso iterativo. Al procesar las primeras versiones de la gramática con el ply se encontraban gran número de conflictos. Se fue modificando la gramática, resolviendo ambigüedades para eliminar conflictos shift/reduce y reduce/reduce encontrados, lo que hizo que la misma creciera en cantidad de producciones y en complejidad.
Una vez eliminados los conflictos que no podían ser solucionados seleccionando un acción  de la tabla LALR, se procedió a ejecutar tests mas complejos provistos por la cátedra para terminar de pulir los últimos detalles. Dado que la gramática es ambigua no es LR(1) y por ende no es LALR. 
    En las siguientes secciones se presentan las producciones de la gramática, solución de conflicto de if-else mediante selección de acción y chequeo de tipos. 
    

\subsection{Producciones gramática}\label{sec:pgram}
\begin{verbatim}
    <programa>      :   <sentencia> <programa'>   
                    |   <control> <programa'>   
                    |   COMENTARIO <programa>   

    <programa'>     :   <sentencia> <programa'>   
                    |   <control> <programa'>   
                    |   COMENTARIO <programa'>   
                    |   <empty>

    <sentencia>     :   <var_asig> ';'
                    |   <funcion> ';'

    <control>       :   <ifelse>
                    |   <loop>

    <control_cond>  :   <var_asig_l>
                    |   <exp_bool>
                    |   <comparacion>
                    |   <op_ternario>

    <loop>          :   'while' '(' <control_cond> ')' <bloque>
                    |   'do' <bloque> 'while' '(' <control_cond> ')' ';' 
                    |   <for>

    <for>           :   'for' '(' <for_term> ',' <form_term_2> ',' <for_term> ')' 
                                <bloque>


    <for_term>      :   <var_asig> 
                    |   <empty>

    <form_term_2>   :   <valores>
                    |   <comparacion>

    <ifelse>        :   'if' '(' <control_cond> ')' <bloque>
                    |   'if' '(' <control_cond> ')' <bloque> else <bloque>

    <bloque>        :   COMENTARIO <bloque>
                    |   <sentencia>
                    |   <control>
                    |   '{' <programa'> '}'

    <funcion>       :   func_ret
                    |   func_void

    <func_void>     :   'print' '(' <valores> ')'

    <func_ret>      :   <func_ret_int>
                    |   <func_ret_cadena>
                    |   <func_ret_bool>
                    |   <func_ret_arreglo>

<func_ret_arreglo>  :   'multiplicarEscalar' '(' <valores> ',' <valores> ')'
                    |   'multiplicarEscalar' '(' <valores> ',' <valores> ',' 
                                                 <valores>')'
                    
<func_ret_bool>     :   'colineales' '(' <valores> ',' <valores> ')'

<func_ret_cadena>   :   'capitalizar' '(' <valores> ')'

    <func_ret_int>  :   'length' '(' valores ')'


    <valores>       :   <exp_arit>
                    |   <exp_bool>
                    |   <exp_cadena>
                    |   <exp_arreglo>
                    |   <registro>
                    |   <registro> '.' VARIABLE
                    |   <var_asig_l>
                    |   <op_ternario>

    <exp_arreglo>   :   '[' <list_valores> ']'
                    |   '[' <list_valores> ']' <exp_arreglo>
                    |   '['']'
                    |   func_ret_arreglo

    <lista_valores> :  <valores>
                    |  <valores> ',' <lista_valores>

    <registro>      :   '{' <reg_item> '}'

    <reg_item>      :   VARIABLE ':' <valores> ',' <reg_item>
                    |   VARIABLE ':' <valores> 

    <var_asig_l>    :   VARIABLE
                    |   RES
                    |   VARIABLE <var_member>


    <var_member>    :   '[' var_asig_l ']' <var_member>
                    |   '[' <exp_arit> ']'  <var_member>
                    |   '.' VARIABLE <var_member>
                    |   '[' <exp_arit> ']' 
                    |   '[' <var_asig_l> ']' 
                    |   '.' VARIABLE 


    <var_asig>      :   <var_asig_l> '++'
                    |   '++' <var_asig_l>
                    |   <var_asig_l> '--'
                    |   '--' <var_asig_l>
                    |   <var_asig_l> '*=' <valores>
                    |   <var_asig_l> '/=' <valores>
                    |   <var_asig_l> '+=' <valores>
                    |   <var_asig_l> '-=' <valores>
                    |   <var_asig_l> '=' <valores>
                    |   <var_asig_l> '=' <comparacion>
                    |   <var_asig_l> '=' <operador_ternario>

    <op_ternario>   :   <valores> '?' <valores> ':' <valores>
                    |   <comparacion> '?' <valores> ':' <valores>
                    |   <valores> '?' <valores> ':' <op_ternario>
                    |   <comparacion> '?' <valores> ':' <op_ternario> 
                
    <var_oper>      :   <var_asig_l>
                    |   '(' <op_ternario> ')'
                    |   <exp_arreglo>
                    |   <registro> '.' VARIABLE 

 
    <exp_arit>      :   <exp_arit> '+' <term>
                    |   <exp_arit> '+' <var_oper>
                    |   <var_oper> '+' <term>
                    |   <var_oper> '+' <var_oper>
                    |   <exp_arit> '-' <term>
                    |   <exp_arit> '-' <var_oper>
                    |   <var_oper> '-' <term>
                    |   <var_oper> '-' <var_oper>
                    |   <term>


    <arit_oper_2>   :   '*'
                    |   '/'
                    |   '%'
    
    <term>          :   <term>  <arti_oper_2> <factor>
                    |   <var_oper> <arit_oper_2> <factor>
                    |   <term> <arit_oper_2> <var_oper>
                    |   <var_oper> <arit_oper_2> <var_oper> 
                    |   <factor>

    <factor>        :   <base> '^' <sigexp>
                    |   <var_oper> '^' <sigexp>
                    |   '-' <base>  
                    |   '-' <var_oper>  
                    |   <base> '++'
                    |   <base> '--'
                    |   '++' <base>
                    |   '--'<base>
                    |   <var_oper> '++'
                    |   '++' <var_oper>
                    |   <var_oper> '--'
                    |   '--' <var_oper>
                    |   <base>

    <base>          :   '(' <exp_arit> ')'
                    |   '(' <var_oper> ')'
                    |   NUMBER
                    |   <func_int>

    <sigexp>        :   '-' <exp>
                    |   <exp>

    <exp>           :   <var_oper>
                    |   NUMBER
                    |   '(' <exp_arit> ')'

    <exp_cadena>    :   <exp_cadena> '+' <term_cadena>
                    |   <var_oper> '+' <term_cadena>
                    |   <term_cadena> '+' <var_oper> 
                    |   <term_cadena> 

    <term_cadena>   :   CADENA
                    |   <func_ret_cadena>
                    |   '(' <exp_cadena> ')'



    <exp_bool>      :   <exp_bool> AND <term_bool>
                    |   <var_oper> AND <term_bool>
                    |   <exp_bool> AND <var_oper>
                    |   <var_oper> AND <var_oper>
                    |   <term_bool>

    <term_bool>     :   <term_bool> OR <factor_bool>
                    |   <var_oper> OR <factor_bool>
                    |   <term_bool> OR <var_oper>
                    |   <var_oper> OR <var_oper>
                    |   'not' <factor_bool>
                    |   'not' <var_oper>

    <factor_bool>   :   BOOL
                    |   '(' <exp_bool> ')'
                    |   '(' <comparacion> ')'
                    |   <func_bool>

    <op_comp>       :   '=='
                    |   '>'
                    |   '<'
                    |   '!='

    <comparacion>   :   <valores> <op_comp>  <valores>

    
\end{verbatim}
\subsection{Resolución de conflicto if-else}\label{sec:pgram}
Nuestra gramática presenta la ambigüedad conocida como "dangling else". En nuestra gramática una sentencia de la forma "if E1 B1 else if E2 B2 else B3" tiene dos arboles de parseo posibles cuando <bloque> deriva en <sentencia> , ver figura ~\ref{fig:trees} .  En este caso se opto por resolver el conflicto seleccionando la acción shift de la tabla, dado que con dicha selección se logra el comportamiento deseado de hacer matchear el "else" con el anterior if sin matchear.  

\begin{figure}[H]

    \begin{subfigure}[b]{0.3\textwidth}
    \begin{tikzpicture}[every node/.style={align=center},level distance=1.5cm]
    \Tree [ .<ifelse> \textbf{if} [ .<control\_cond> \edge[roof]; {E1}  ] [ .<bloque> [ .<sentencia> \textbf{if} [ .<control\_cond> \edge[roof]; {E2}  ]  ] [ .<bloque> [ .<sentencia> \edge[roof]; {B1}  ] ] ] \textbf{else}  [ .<bloque> [ .<sentencia> \edge[roof]; {B2}  ] ] ] 
    \end{tikzpicture}
    \end{subfigure}
    
    \begin{subfigure}[b]{0.3\textwidth}
    \begin{tikzpicture}[every node/.style={align=center},level distance=1.5cm]
    \Tree [ .<ifelse> \textbf{if} [ .<control\_cond> \edge[roof]; {E1}  ] [ .<bloque> [ .<sentencia> \textbf{if} [ .<control\_cond> \edge[roof]; {E2}  ] [ .<bloque> [ .<sentencia> \edge[roof]; {B1}  ] ] \textbf{else}  [ .<bloque> [ .<sentencia> \edge[roof]; {B2}  ] ] ] ] ]
    \end{tikzpicture}
    \end{subfigure}
    



     \caption{Versión simplificada de arboles de parseo para \\"if E1 B1 else if E2 B2 else B3 }
      \label{fig:trees}
\end{figure}    
\subsection{Chequeo de tipos}

Unos de los objetivos de la implementación era verificar que el código cumpliese con restricciones de tipado del lenguaje. Parte de este chequeo se hace a través de la sintaxis no permitiendo  que se formen cadenas que violen dichas restricciones. 
Por ejemplo las producciones mostradas a continuación del caso  base de exp\_arit no aseguran que los tokens BOOL y CADENA no van a aparecer en una expresión aritmética. 
\begin{verbatim}
    <base>          :   '(' <exp_arit> ')'
                    |   '(' <var_oper> ')'
                    |   NUMBER
                    |   <func_int>

\end{verbatim}
	Ahora bien, no es posible chequear con esta gramática expresiones donde se usan variables . Para ellos se extendió la gramática a una una gramática de atributos para poder poder hacer una validación más completa. Esto se logra mediante la síntesis del atributo tipo y chequeando que se cumplan las restricciones en el momento que se utilizan las variables. Esta extensión no solo permitió hacer un chequeo mas completo sino adema permitió simplificar algunas producciones por ejemplo el de las comparaciones donde las producciones:
    
\begin{verbatim}
<comparacion>   :   <exp_cadena> <op_comp>  <exp_cadena>
                 |   <var_oper> <op_comp>  <exp_cadena>
                 |   <exp_cadena> <op_comp> <var_oper> 
                 |   <exp_arit>   <op_comp>   <exp_arit>
                 |   <var_oper>   <op_comp>   <exp_arit>
                 |   <exp_arit>   <op_comp>   <var_oper>
                 |   <var_oper>   <op_comp>  <var_oper>
                 |   <exp_bool> '==' <exp_bool>
                 |   <exp_bool> '!=' <exp_bool>
\end{verbatim}
fueron reemplazadas por una sola producción:
\begin{verbatim}
 <comparacion>   :   <valores> <op_comp>  <valores>
\end{verbatim}
 
Otro caso en el que la síntesis del atributo tipo resulto útil fue para expresiones que son suma de todas variables. Dicha expresión es válida tanto para cadenas como para números, pero solo se puede derivar a partir de <exp\_arit>. Utilizando el atributo de tipo, podemos permitir que dicha expresión se forme tanto para suma de cadenas como para suma de variables con números.
    En la siguiente sección se profundiza sobre el análisis semántico.

\input{semantica.tex}
